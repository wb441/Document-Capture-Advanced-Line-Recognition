OBJECT Table 6085580 Template Field
{
  OBJECT-PROPERTIES
  {
    Date=12/20/17;
    Time=10:21:02 PM;
    Modified=Yes;
    Version List=DCW13.70.00.4.50.01,CKL;
  }
  PROPERTIES
  {
    DataCaptionFields=Type,Code,Field Name;
    OnInsert=VAR
               Template@1000000000 : Record 6085579;
             BEGIN
               IF "Language Code" = '' THEN BEGIN
                 Template.GET("Template No.");
                 "Language Code" := Template."Language Code";
               END;
             END;

    OnDelete=VAR
               TemplField@161024012 : Record 6085580;
               Rule@1000000002 : Record 6085583;
               Caption@1000000000 : Record 6085581;
               Translation@1000000001 : Record 6085582;
               DataTransl@1160040001 : Record 6085584;
               RecIDMgt@1160040000 : Codeunit 6085604;
             BEGIN
               TemplField.SETRANGE("Template No.","Template No.");
               TemplField.SETRANGE(Type,Type);
               TemplField.SETRANGE("G/L Account Field Code","G/L Account Field Code");
               IF TemplField.COUNT = 1 THEN
                 IF TemplField.GET("Template No.",Type,"G/L Account Field Code") THEN
                   TemplField.DELETE(TRUE);

               Rule.SETRANGE("Template No.","Template No.");
               Rule.SETRANGE(Type,Type);
               Rule.SETRANGE(Code,Code);
               Rule.DELETEALL(TRUE);

               Caption.SETRANGE("Template No.","Template No.");
               Caption.SETRANGE(Type,Type);
               Caption.SETRANGE(Code,Code);
               Caption.DELETEALL(TRUE);

               Translation.SETRANGE("Template No.","Template No.");
               Translation.SETRANGE(Type,Type);
               Translation.SETRANGE(Code,Code);
               Translation.DELETEALL(TRUE);

               DataTransl.SETRANGE("Template No.","Template No.");
               DataTransl.SETRANGE(Type,Type);
               DataTransl.SETRANGE("Field Code",Code);
               DataTransl.DELETEALL(TRUE);

               RecIDMgt.DeleteTableFilter("Source Table Filter GUID");
             END;

    CaptionML=[DEU=Vorlagenfeld;
               ENU=Template Field];
    LookupPageID=Page6085602;
    DrillDownPageID=Page6085602;
  }
  FIELDS
  {
    { 1   ;   ;Template No.        ;Code20        ;TableRelation=Template;
                                                   CaptionML=[DEU=Vorlagennr.;
                                                              ENU=Template No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Type                ;Option        ;CaptionML=[DEU=Typ;
                                                              ENU=Type];
                                                   OptionCaptionML=[DEU=Kopf,Zeile;
                                                                    ENU=Header,Line];
                                                   OptionString=Header,Line }
    { 3   ;   ;Code                ;Code20        ;CaptionML=[DEU=Code;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 4   ;   ;Data Type           ;Option        ;OnValidate=VAR
                                                                GLSetup@161024012 : Record 98;
                                                                Template@161024013 : Record 6085579;
                                                              BEGIN
                                                                SetFixedValue('');

                                                                IF "Data Type" <> "Data Type"::Lookup THEN
                                                                  VALIDATE("Source Table No.",0);

                                                                IF "Data Type" = "Data Type"::Number THEN BEGIN
                                                                  GLSetup.GET;
                                                                  VALIDATE("Decimal Places",GLSetup."Amount Decimal Places");
                                                                  VALIDATE("Blank Zero",TRUE);
                                                                END;

                                                                IF "Data Type" = "Data Type"::Date THEN BEGIN
                                                                  Template.GET("Template No.");
                                                                  "Date Format" := Template."Date Format" + 1;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Datentyp;
                                                              ENU=Data Type];
                                                   OptionCaptionML=[DEU=Text,Nummer,Datum,Boolesch,,,,,Lookup;
                                                                    ENU=Text,Number,Date,Boolean,,,,,Lookup];
                                                   OptionString=Text,Number,Date,Boolean,,,,,Lookup }
    { 5   ;   ;Required            ;Boolean       ;CaptionML=[DEU=Erforderlich;
                                                              ENU=Required] }
    { 6   ;   ;Search for Value    ;Boolean       ;CaptionML=[DEU=Nach Wert suchen;
                                                              ENU=Search for Value] }
    { 7   ;   ;Caption Is Part Of Value;Boolean   ;CaptionML=[DEU=Caption ist Teil des Wertes;
                                                              ENU=Caption Is Part Of Value] }
    { 8   ;   ;Insert on new Templates;Boolean    ;InitValue=Yes;
                                                   CaptionML=[DEU=In neue Vorlage einfÅgen;
                                                              ENU=Insert on new Templates] }
    { 9   ;   ;Number Has Decimals ;Boolean       ;CaptionML=[DEU=Zahl enthÑlt Dezimalstellen;
                                                              ENU=Number Has Decimals] }
    { 10  ;   ;Caption Offset X    ;Integer       ;CaptionML=[DEU=Caption Offset X;
                                                              ENU=Caption Offset X] }
    { 11  ;   ;Caption Offset Y    ;Integer       ;CaptionML=[DEU=Caption Offset Y;
                                                              ENU=Caption Offset Y] }
    { 12  ;   ;Fixed Value (Text)  ;Text250       ;CaptionML=[DEU=Fester Wert (Text);
                                                              ENU=Fixed Value (Text)] }
    { 13  ;   ;Fixed Value (Decimal);Decimal      ;CaptionML=[DEU=Fester Wert (Dezimal);
                                                              ENU=Fixed Value (Decimal)] }
    { 14  ;   ;Fixed Value (Date)  ;Date          ;CaptionML=[DEU=Fester Wert (Datum);
                                                              ENU=Fixed Value (Date)] }
    { 15  ;   ;Fixed Value (Lookup);Code20        ;TableRelation=IF (Lookup Table=CONST(Vendor)) Vendor
                                                                 ELSE IF (Lookup Table=CONST(Contact)) Contact
                                                                 ELSE IF (Lookup Table=CONST(Customer)) Customer
                                                                 ELSE IF (Lookup Table=CONST(Job)) Job
                                                                 ELSE IF (Lookup Table=CONST(Item)) Item
                                                                 ELSE IF (Lookup Table=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Lookup Table=CONST(Employee)) Employee
                                                                 ELSE IF (Lookup Table=CONST(Dimension Value)) "Dimension Value".Code WHERE (Dimension Code=FIELD(Lookup Rel. Code));
                                                   CaptionML=[DEU=Fester Wert (Lookup);
                                                              ENU=Fixed Value (Lookup)] }
    { 16  ;   ;Field Type          ;Option        ;CaptionML=[DEU=Feldart;
                                                              ENU=Field Type];
                                                   OptionCaptionML=[DEU=Text,Barcode;
                                                                    ENU=Text,Barcode];
                                                   OptionString=Text,Barcode }
    { 17  ;   ;Validation Dateformula From;DateFormula;
                                                   CaptionML=[DEU=Validierung Datumsformel von;
                                                              ENU=Validation Dateformula From] }
    { 18  ;   ;Validation Dateformula To;DateFormula;
                                                   CaptionML=[DEU=Validierung Datumsformel bis;
                                                              ENU=Validation Dateformula To] }
    { 19  ;   ;Never use Global Captions;Boolean  ;InitValue=Yes;
                                                   CaptionML=[DEU=Globale Captions nie benutzen;
                                                              ENU=Never use Global Captions] }
    { 20  ;   ;Decimal Places      ;Code10        ;CaptionML=[DEU=Dezimalstellen;
                                                              ENU=Decimal Places] }
    { 21  ;   ;Blank Zero          ;Boolean       ;CaptionML=[DEU=Blank Zero;
                                                              ENU=Blank Zero] }
    { 22  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[DEU=Sprachcode;
                                                              ENU=Language Code] }
    { 23  ;   ;Enable Rule Generation;Boolean     ;CaptionML=[DEU=Regelerzeugung aktivieren;
                                                              ENU=Enable Rule Generation] }
    { 24  ;   ;Multiline           ;Boolean       ;CaptionML=[DEU=Mehrzeilig;
                                                              ENU=Multiline] }
    { 25  ;   ;Default Page Source ;Option        ;CaptionML=[DEU=Vorgabe Seitenherkunft;
                                                              ENU=Default Page Source];
                                                   OptionCaptionML=[DEU=Erste Seite,Letzte Seite;
                                                                    ENU=First Page,Last Page];
                                                   OptionString=First Page,Last Page }
    { 26  ;   ;Auto Update Caption ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU=Caption autom. aktualisieren;
                                                              ENU=Auto Update Caption] }
    { 27  ;   ;Typical Field Width ;Decimal       ;CaptionML=[DEU=Typische Feldbreite;
                                                              ENU=Typical Field Width];
                                                   DecimalPlaces=0:5 }
    { 28  ;   ;Auto Update Field Width;Boolean    ;InitValue=Yes;
                                                   CaptionML=[DEU=Feldbreite automatisch aktualisieren;
                                                              ENU=Auto Update Field Width] }
    { 29  ;   ;Typical Field Height;Decimal       ;CaptionML=[DEU=Typische Feldhîhe;
                                                              ENU=Typical Field Height];
                                                   DecimalPlaces=0:5 }
    { 30  ;   ;Auto Update Field Height;Boolean   ;InitValue=Yes;
                                                   CaptionML=[DEU=Feldhîhe automatisch aktualisieren;
                                                              ENU=Auto Update Field Height] }
    { 31  ;   ;Sort Order          ;Integer       ;CaptionML=[DEU=Sortierung;
                                                              ENU=Sort Order] }
    { 32  ;   ;Field Name          ;Text50        ;CaptionML=[DEU=Feldname;
                                                              ENU=Field Name] }
    { 33  ;   ;Stop Lines Recognition;Option      ;CaptionML=[DEU=Zeilenerkennung stoppen;
                                                              ENU=Stop Lines Recognition];
                                                   OptionCaptionML=[DEU=" ,Falls die Caption in der gleichen Zeile ist,Falls der Wert in der gleichen Zeile ist,Falls die Caption oder der Wert in der gleichen Zeile ist,Falls die Caption in der gleichen Zeile ist (weiter auf nÑchster Seite),Falls der Wert in der gleichen Zeile ist (weiter auf nÑchster Seite),Falls die Caption oder der Wert in der gleichen Zeile ist (weiter auf nÑchster Seite)";
                                                                    ENU=" ,If Caption is on same line,If Value is on same line,If Caption or Value is on same line,If Caption is on same line (continue on next page),If Value is on same line (continue on next page),If Caption or Value is on same line (continue on next page)"];
                                                   OptionString=[ ,If Caption is on same line,If Value is on same line,If Caption or Value is on same line,If Caption is on same line (continue on next page),If Value is on same line (continue on next page),If Caption or Value is on same line (continue on next page)] }
    { 34  ;   ;Make Absolute Number;Boolean       ;CaptionML=[DEU=Absolutbetrag erzeugen;
                                                              ENU=Make Absolute Number] }
    { 35  ;   ;1. Caption          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Template Field Caption".Caption WHERE (Template No.=FIELD(Template No.),
                                                                                                              Type=FIELD(Type),
                                                                                                              Code=FIELD(Code)));
                                                   CaptionML=[DEU=1. Caption;
                                                              ENU=1. Caption];
                                                   Editable=No }
    { 36  ;   ;1. Rule             ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Template Field Rule".Rule WHERE (Template No.=FIELD(Template No.),
                                                                                                        Type=FIELD(Type),
                                                                                                        Code=FIELD(Code)));
                                                   CaptionML=[DEU=1. Regel;
                                                              ENU=1. Rule];
                                                   Editable=No }
    { 37  ;   ;Lookup Table        ;Option        ;CaptionML=[DEU=Lookup Tabelle;
                                                              ENU=Lookup Table];
                                                   OptionCaptionML=[DEU=" ,Kreditor,Kontakt,Debitor,Projekt,Artikel,Anlage,Mitarbeiter,Dimensionswert";
                                                                    ENU=" ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee,Dimension Value"];
                                                   OptionString=[ ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee,Dimension Value] }
    { 38  ;   ;Lookup Rel. Code    ;Code20        ;TableRelation=IF (Lookup Table=CONST(Dimension Value)) Dimension;
                                                   CaptionML=[DEU=Relation Code;
                                                              ENU=Relation Code] }
    { 39  ;   ;Codeunit ID: Is OK  ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit ID: Ist OK;
                                                              ENU=Codeunit ID: Is OK];
                                                   BlankZero=Yes }
    { 41  ;   ;Codeunit ID: Lookup ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit ID: Lookup;
                                                              ENU=Codeunit ID: Lookup];
                                                   BlankZero=Yes }
    { 43  ;   ;Codeunit ID: Validate;Integer      ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit ID: Validate;
                                                              ENU=Codeunit ID: Validate];
                                                   BlankZero=Yes }
    { 45  ;   ;Formula             ;Code250       ;OnValidate=VAR
                                                                CaptureMgt@1160040000 : Codeunit 6085576;
                                                                WebClientManagement@6085574 : Codeunit 6085662;
                                                              BEGIN
                                                                IF Formula = '' THEN
                                                                  EXIT;

                                                                IF ("Data Type" = "Data Type"::Number) AND NOT WebClientManagement.IsWebClient THEN
                                                                  CaptureMgt.ValidateNumberFormula("Template No.",Formula);

                                                                "Search for Value" := FALSE;
                                                              END;

                                                   CaptionML=[DEU=Formel;
                                                              ENU=Formula] }
    { 46  ;   ;G/L Account Field Code;Code20      ;TableRelation="Template Field".Code WHERE (Template No.=FIELD(Template No.),
                                                                                              Type=CONST(Header),
                                                                                              Data Type=CONST(Text));
                                                   CaptionML=[DEU=Konto Feldcode;
                                                              ENU=G/L Account Field Code] }
    { 47  ;   ;Transfer Amount to Document;Option ;CaptionML=[DEU=Betrag an Beleg Åbertragen;
                                                              ENU=Transfer Amount to Document];
                                                   OptionCaptionML=[DEU=" ,falls Zeilen nicht erkannt wurden,Immer";
                                                                    ENU=" ,If lines are not recognised,Always"];
                                                   OptionString=[ ,If lines are not recognised,Always] }
    { 48  ;   ;Subtract from Amount Field;Code20  ;TableRelation="Template Field".Code WHERE (Template No.=FIELD(Template No.),
                                                                                              Type=FIELD(Type),
                                                                                              Transfer Amount to Document=FILTER(<>' '));
                                                   OnValidate=BEGIN
                                                                IF "Subtract from Amount Field" = Code THEN
                                                                  ERROR(Text003);
                                                              END;

                                                   CaptionML=[DEU=Betrag bei Registrierung abziehen von;
                                                              ENU=Subtract from Amount Field (on registration)] }
    { 49  ;   ;Codeunit ID: Capture Value;Integer ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit ID: Capture Wert;
                                                              ENU=Codeunit ID: Capture Value];
                                                   BlankZero=Yes }
    { 51  ;   ;Purch. Alloc. G/L Account No.;Code20;
                                                   TableRelation="G/L Account";
                                                   CaptionML=[DEU=Eink. Zuordnungssachkonto;
                                                              ENU=Purch. Alloc. G/L Account No.] }
    { 52  ;   ;Date Format         ;Option        ;CaptionML=[DEU=Datumsformat;
                                                              ENU=Date Format];
                                                   OptionCaptionML=[DEU=" ,Tag / Monat / Jahr,Monat / Tag / Jahr,Jahr / Tag / Monat,Jahr / Monat / Tag,Tag / Jahr / Monat,Monat / Jahr / Tag";
                                                                    ENU=" ,Day / Month / Year,Month / Day / Year,Year / Day / Month,Year / Month / Day,Day / Year / Month,Month / Year / Day"];
                                                   OptionString=[ ,Day / Month / Year,Month / Day / Year,Year / Day / Month,Year / Month / Day,Day / Year / Month,Month / Year / Day] }
    { 53  ;   ;Source Table No.    ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                DCMgt@1160040001 : Codeunit 6085603;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Table No." <> 0 THEN
                                                                  TESTFIELD("Data Type","Data Type"::Lookup);

                                                                DCMgt.IsLogActive2("Source Table No.",TRUE);

                                                                IF "Source Table No." = xRec."Source Table No." THEN
                                                                  EXIT;

                                                                RecIDMgt.CheckDocValue(Type = Type::Line,Code,"Template No.",FIELDCAPTION("Source Table No."));
                                                                RecIDMgt.DeleteTableFilter("Source Table Filter GUID");
                                                                CLEAR("Source Table Filter GUID");
                                                                "Fixed Value (Rec. ID Tree ID)" := 0;
                                                                "Source Field No." := RecIDMgt.GetFirstKeyField("Source Table No.");
                                                              END;

                                                   CaptionML=[DEU=Herkunftstabellennr.;
                                                              ENU=Source Table No.];
                                                   BlankZero=Yes }
    { 55  ;   ;Source Table Filter GUID;GUID      ;CaptionML=[DEU=Herkunftsfeldfilter GUID;
                                                              ENU=Source Table Filter GUID] }
    { 56  ;   ;Source Field No.    ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;

                                                                IF xRec."Source Field No." > 0 THEN
                                                                  RecIDMgt.CheckDocValue(Type = Type::Line,Code,"Template No.",FIELDCAPTION("Source Field No."));

                                                                IF NOT RecIDMgt.PartOfKey("Source Table No.","Source Field No.") THEN
                                                                  ERROR(Text005,FIELDCAPTION("Source Field No."),FIELDCAPTION("Source Table No."));
                                                              END;

                                                   CaptionML=[DEU=Herkunftsfeldnr.;
                                                              ENU=Source Field No.];
                                                   BlankZero=Yes }
    { 57  ;   ;Destination Header Field No.;Integer;
                                                   TableRelation=Field.No.;
                                                   OnValidate=VAR
                                                                Template@1160040001 : Record 6085579;
                                                                Field@161024013 : Record 2000000041;
                                                                DocCat@161024012 : Record 6085575;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF ("Destination Header Field No." = xRec."Destination Header Field No.") OR ("Destination Header Field No." = 0) THEN
                                                                  EXIT;

                                                                Template.GET("Template No.");
                                                                DocCat.GET(Template."Category Code");
                                                                DocCat.TESTFIELD("Destination Header Table No.");

                                                                Field.GET(DocCat."Destination Header Table No.","Destination Header Field No.");
                                                                Field.TESTFIELD(Enabled);
                                                                Field.TESTFIELD(Class,Field.Class::Normal);
                                                              END;

                                                   CaptionML=[DEU=Feldnr.  Zielkopf;
                                                              ENU=Destination Header Field No.];
                                                   BlankZero=Yes }
    { 58  ;   ;No. of Source Table Filters;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Table Filter Field" WHERE (Table Filter GUID=FIELD(Source Table Filter GUID)));
                                                   CaptionML=[DEU=Anzahl von Herkunftstabellenfiltern;
                                                              ENU=No. of Source Table Filters];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 59  ;   ;Fixed Value (Rec. ID Tree ID);Integer;
                                                   TableRelation="Record ID Tree";
                                                   CaptionML=[DEU=Fester Wert (Datensatz-ID Baum-ID);
                                                              ENU=Fixed Value (Rec. ID Tree ID)] }
    { 60  ;   ;Destination Line Field No.;Integer ;TableRelation=Field.No.;
                                                   OnValidate=VAR
                                                                Template@1160040001 : Record 6085579;
                                                                Field@161024013 : Record 2000000041;
                                                                DocCat@161024012 : Record 6085575;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF ("Destination Line Field No." = xRec."Destination Line Field No.") OR ("Destination Line Field No." = 0) THEN
                                                                  EXIT;

                                                                Template.GET("Template No.");
                                                                DocCat.GET(Template."Category Code");
                                                                DocCat.TESTFIELD("Destination Line Table No.");

                                                                Field.GET(DocCat."Destination Line Table No.","Destination Line Field No.");
                                                                Field.TESTFIELD(Enabled);
                                                                Field.TESTFIELD(Class,Field.Class::Normal);
                                                              END;

                                                   CaptionML=[DEU=Feldnr. Zielzeile;
                                                              ENU=Destination Line Field No.];
                                                   BlankZero=Yes }
    { 61  ;   ;Delete Blanks       ;Boolean       ;CaptionML=[DEU=Leerzeichen lîschen;
                                                              ENU=Delete Blanks] }
    { 62  ;   ;Caption Mandatory   ;Boolean       ;CaptionML=[DEU=Caption notwendig;
                                                              ENU=Caption Mandatory] }
    { 63  ;   ;Use as Doc. Separator;Boolean      ;OnValidate=BEGIN
                                                                IF NOT "Use as Doc. Separator" THEN
                                                                  EXIT;

                                                                TESTFIELD(Type,Type::Header);
                                                              END;

                                                   CaptionML=[DEU=Als Belegtrenner verwenden;
                                                              ENU=Use as Document Separator] }
    { 64  ;   ;Fixed Value (Boolean);Boolean      ;CaptionML=[DEU=Fester Wert (Boolesch);
                                                              ENU=Fixed Value (Boolean)] }
    { 65  ;   ;Offset DPI          ;Integer       ;CaptionML=[DEU=Offset DPI;
                                                              ENU=Offset DPI] }
    { 66  ;   ;Typical Field DPI   ;Integer       ;CaptionML=[DEU=Typische Feld DPI;
                                                              ENU=Typical Field DPI] }
    { 67  ;   ;Transfer Blank Values;Boolean      ;CaptionML=[DEU=Leere Werte Åbertragen;
                                                              ENU=Transfer Blank Values] }
    { 50000;  ;Field valid at min Top;Integer     ;CaptionML=DEU=Feld gÅltig bei mind. x-Position }
    { 50001;  ;Field valid at min Left;Integer    ;CaptionML=DEU=Feld gÅltig bei mind. y-Position }
    { 50010;  ;Line Identification Field;Boolean  ;CaptionML=[DEU=Zeilenidentifikationsfeld;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 50011;  ;Field Calculation Type;Option      ;CaptionML=DEU=Feldberechnungsart;
                                                   OptionCaptionML=[DEU=Standard,Zeilen Identifikations Feld;
                                                                    ENU=Standard,Line identification field];
                                                   OptionString=Default,LineIdentificationField,HeaderCaption,FooterCaption,Flexline }
    { 50012;  ;Offset Top          ;Integer       ;CaptionML=DEU=Offset Oben }
    { 50013;  ;Offset Bottom       ;Integer       ;CaptionML=DEU=Offset Unten }
    { 50014;  ;Offset Left         ;Integer       ;CaptionML=DEU=Offset Links }
    { 50015;  ;Offset Right        ;Integer       ;CaptionML=DEU=Offset Rechts }
  }
  KEYS
  {
    {    ;Template No.,Type,Code                  ;Clustered=Yes }
    {    ;Template No.,Type,Sort Order             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'DEU=Es ist nicht ratsam, Leerzeichen als Trennzeichen fÅr Datumsvalidierungsregeln zu benutzen, da sie vor der Verarbeitung gelîscht werden. Benutzen Sie stattdessen einen Bindestrich - oder einen SchrÑgstrich /.;ENU=It is not recommended to use space as a seperator in Date Validation Rules as they will be deleted before evaluation. Use a seperator line - or / instead.';
      Text002@161024012 : TextConst 'DEU=Mîchten Sie auch das Feld ''%1'' lîschen?;ENU=Do you also want to delete the field ''%1''?';
      Text003@161024013 : TextConst 'DEU=Das Feld kann nicht aus sich selbst erzeugt werden.;ENU=The field cannot be subtracted from itself';
      Text004@1160040000 : TextConst 'DEU=Der Datensatz ist nicht in den Filtern von %1 enthalten.;ENU=The record is not within the filters of the %1.';
      Text005@1160040001 : TextConst 'DEU=%1 ist kein Teil des PrimÑrschlÅssels von %2.;ENU=The %1 is not part of the primary key of the %2.';
      Text006@1160040002 : TextConst 'DEU=Feld in %1;ENU=Field in %1';
      Text007@1160040011 : TextConst 'DEU=Feld Zielkopf;ENU=Destination Header Field';
      Text008@1160040010 : TextConst 'DEU=Feld Zielzeile;ENU=Destination Line Field';

    PROCEDURE Clone@161024012(FromCompany@1160040002 : Text[30];FromField@1160040003 : Record 6085580;ToTemplate@161024013 : Record 6085579);
    VAR
      ToField@1000000002 : Record 6085580;
      FromFieldRule@1000000010 : Record 6085583;
      ToFieldRule@1000000009 : Record 6085583;
      FromFieldCapt@1000000003 : Record 6085581;
      ToFieldCaption@1000000004 : Record 6085581;
      FromFieldTransl@1000000005 : Record 6085582;
      ToFieldTransl@1000000006 : Record 6085582;
      FromTableFilterFields@1160040000 : Record 6085762;
      ToTableFilterFields@1160040001 : Record 6085762;
    BEGIN
      ToField := FromField;
      ToField."Template No." := ToTemplate."No.";
      ToField."Fixed Value (Rec. ID Tree ID)" := 0;
      IF ToField.FIND('=') THEN
        EXIT;

      ToField.INSERT(TRUE);

      IF FromCompany <> '' THEN BEGIN
        FromFieldRule.CHANGECOMPANY(FromCompany);
        FromFieldCapt.CHANGECOMPANY(FromCompany);
        FromFieldTransl.CHANGECOMPANY(FromCompany);
        FromTableFilterFields.CHANGECOMPANY(FromCompany);
      END;

      FromFieldRule.SETRANGE("Template No.",FromField."Template No.");
      FromFieldRule.SETRANGE(Type,FromField.Type);
      FromFieldRule.SETRANGE(Code,FromField.Code);
      IF FromFieldRule.FINDSET THEN
        REPEAT
          ToFieldRule := FromFieldRule;
          ToFieldRule."Template No." := ToField."Template No.";
          ToFieldRule.INSERT(TRUE);
        UNTIL FromFieldRule.NEXT = 0;

      FromFieldCapt.SETRANGE("Template No.",FromField."Template No.");
      FromFieldCapt.SETRANGE(Type,FromField.Type);
      FromFieldCapt.SETRANGE(Code,FromField.Code);
      IF FromFieldCapt.FINDSET THEN
        REPEAT
          ToFieldCaption := FromFieldCapt;
          ToFieldCaption."Template No." := ToField."Template No.";
          ToFieldCaption.INSERT(TRUE);
        UNTIL FromFieldCapt.NEXT = 0;

      FromFieldTransl.SETRANGE("Template No.",FromField."Template No.");
      FromFieldTransl.SETRANGE(Type,FromField.Type);
      FromFieldTransl.SETRANGE(Code,FromField.Code);
      IF FromFieldTransl.FINDSET THEN
        REPEAT
          ToFieldTransl := FromFieldTransl;
          ToFieldTransl."Template No." := ToField."Template No.";
          ToFieldTransl.INSERT(TRUE);
        UNTIL FromFieldTransl.NEXT = 0;

      FromTableFilterFields.SETRANGE("Table Filter GUID",FromField."Source Table Filter GUID");
      IF FromTableFilterFields.FINDSET THEN BEGIN
        ToField."Source Table Filter GUID" := CREATEGUID;
        ToField.MODIFY;
        REPEAT
          ToTableFilterFields := FromTableFilterFields;
          ToTableFilterFields."Table Filter GUID" := ToField."Source Table Filter GUID";
          ToTableFilterFields.INSERT(TRUE);
        UNTIL FromTableFilterFields.NEXT = 0;
      END;
    END;

    PROCEDURE SetFixedValue@1000000000(Value@1000000000 : Text[250]);
    VAR
      RecIDMgt@161024013 : Codeunit 6085604;
      DCMgt@1160040000 : Codeunit 6085603;
      CaptureMgt@1160040001 : Codeunit 6085576;
      RecID@161024012 : Text[30];
    BEGIN
      IF ("Source Table No." <> 0) OR ("Data Type" = "Data Type"::Lookup) THEN BEGIN
        "Fixed Value (Text)" := '';
        IF Value <> '' THEN
          DCMgt.IsLogActive("Source Table No.",TRUE);
        "Fixed Value (Rec. ID Tree ID)" := RecIDMgt.SetKeyValueWithGUID("Source Table No.","Source Field No.",0,
          "Source Table Filter GUID",Value,TRUE);
      END;

      CASE "Data Type" OF
        "Data Type"::Text:
          EVALUATE("Fixed Value (Text)",Value);
        "Data Type"::Number:
          IF Value = '' THEN
            "Fixed Value (Decimal)" := 0
          ELSE
            EVALUATE("Fixed Value (Decimal)",Value);
        "Data Type"::Date:
          IF Value = 'TODAY' THEN
            EVALUATE("Fixed Value (Text)",Value)
          ELSE BEGIN
            "Fixed Value (Text)" := '';
            EVALUATE("Fixed Value (Date)",Value);
          END;
        "Data Type"::Boolean:
          IF Value = '' THEN
            "Fixed Value (Boolean)" := FALSE
          ELSE
            CaptureMgt.ParseBoolean(Value,"Fixed Value (Boolean)");
      END;

      IF Value <> '' THEN
        "Search for Value" := FALSE;
    END;

    PROCEDURE GetFixedValue@1000000001() : Text[250];
    VAR
      RecIDMgt@161024012 : Codeunit 6085604;
    BEGIN
      IF ("Source Table No." <> 0) OR ("Data Type" = "Data Type"::Lookup) THEN
        EXIT(RecIDMgt.GetKeyValue("Fixed Value (Rec. ID Tree ID)","Source Field No."));

      CASE "Data Type" OF
        "Data Type"::Text:
          EXIT(FORMAT("Fixed Value (Text)"));
        "Data Type"::Date:
          IF "Fixed Value (Date)" <> 0D THEN
            EXIT(FORMAT("Fixed Value (Date)"))
          ELSE
            EXIT("Fixed Value (Text)");
        "Data Type"::Number:
          IF "Fixed Value (Decimal)" <> 0 THEN
            EXIT(FORMAT("Fixed Value (Decimal)"));
        "Data Type"::Boolean:
          EXIT(FORMAT("Fixed Value (Boolean)"));
      END;
    END;

    PROCEDURE FormulaOnLookup@161024013(VAR Text@161024012 : Text[1024]) : Boolean;
    VAR
      TemplField@161024013 : Record 6085580;
    BEGIN
      TemplField.FILTERGROUP(2);
      TemplField.SETCURRENTKEY("Template No.",Type,"Sort Order");
      TemplField.SETRANGE("Template No.","Template No.");
      TemplField.SETRANGE(Type,Type);
      TemplField.SETFILTER(Code,'<>%1',Code);
      IF "Data Type" = "Data Type"::Number THEN
        TemplField.SETRANGE("Data Type","Data Type");
      TemplField.FILTERGROUP(0);
      IF STRLEN(Text) <= MAXSTRLEN(TemplField.Code) THEN
        IF TemplField.GET("Template No.","Field Type") THEN;

      IF PAGE.RUNMODAL(0,TemplField) = ACTION::LookupOK THEN BEGIN
        Text := TemplField.Code;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE CheckSourceTableFilter@1160040003();
    VAR
      RecIDMgt@1160040001 : Codeunit 6085604;
      RecRef@1160040000 : RecordRef;
      FieldRef@1160040003 : FieldRef;
      Exist@1160040002 : Boolean;
    BEGIN
      IF "Source Table No." = 0 THEN
        EXIT;

      RecRef.GETTABLE(Rec);

      IF NOT RecIDMgt.IsValueWithinFilters("Source Table No.","Source Field No.",
        "Source Table Filter GUID",RecIDMgt.GetFieldValue("Fixed Value (Rec. ID Tree ID)","Source Field No."))
      THEN
        ERROR(Text004,FIELDCAPTION("Source Table No."));
    END;

    PROCEDURE GetDestFieldCaption@1160040000(FieldType@1160040002 : 'Header,Line') FieldCap : Text[250];
    VAR
      Template@1160040001 : Record 6085579;
      DocCat@1160040000 : Record 6085575;
      AllObjWithCaption@1160040004 : Record 2000000058;
      RecIDMgt@1160040003 : Codeunit 6085604;
    BEGIN
      IF Template.GET("Template No.") THEN
        DocCat.GET(Template."Category Code");

      IF FieldType = FieldType::Header THEN BEGIN
        FieldCap := RecIDMgt.GetObjectCaption(AllObjWithCaption."Object Type"::Table,DocCat."Destination Header Table No.");
        IF FieldCap = '' THEN
          FieldCap := Text007
        ELSE
          FieldCap := STRSUBSTNO(Text006,FieldCap);
      END ELSE BEGIN
        FieldCap := RecIDMgt.GetObjectCaption(AllObjWithCaption."Object Type"::Table,DocCat."Destination Line Table No.");
        IF FieldCap = '' THEN
          FieldCap := Text008
        ELSE
          FieldCap := STRSUBSTNO(Text006,FieldCap);
      END;
    END;

    BEGIN
    {
      // CKL Software GmbH >>>
      // SR: New Fields 50000..50015
      // CKL Software GmbH <<<
    }
    END.
  }
}

